// ================
// Gradle configuration

buildscript {
	repositories {
		jcenter()
		mavenCentral()
		maven { url "https://plugins.gradle.org/m2/" }
	}
}

plugins {
	id 'java-library'
	id 'org.jetbrains.kotlin.jvm' version '1.3.21'
	id 'org.jmailen.kotlinter' version '1.21.0'
	id 'io.gitlab.arturbosch.detekt' version '1.0.0-RC14'
	id 'jacoco'
	id 'org.jetbrains.dokka' version '0.9.17'
	id 'com.jfrog.bintray' version '1.8.4'
	id 'maven-publish'
}

apply from: "${rootProject.projectDir}/gradle/version.gradle"
apply from: "${rootProject.projectDir}/gradle/sourceArtifact.gradle"
apply from: "${rootProject.projectDir}/gradle/bintray.gradle"

wrapper {
	gradleVersion = '5.2.1'
	distributionType = Wrapper.DistributionType.ALL
}

// ================
// Project metadata configuration

group = 'org.ws2ten1'

// ================
// Build configuration

repositories {
	jcenter()
	mavenCentral()
	maven { url 'https://dl.bintray.com/ws2ten1/maven-release' }
}

dependencies {
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	implementation 'com.willowtreeapps.assertk:assertk-jvm:0.13' // https://www.kotlinresources.com/library/assertk/
	implementation 'com.jayway.jsonpath:json-path:2.4.0'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'

	// build
	detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.0.0-RC14"
}

compileKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
		allWarningsAsErrors = true
	}
}

compileTestKotlin {
	kotlinOptions {
		freeCompilerArgs = ['-Xjsr305=strict']
		jvmTarget = '1.8'
		allWarningsAsErrors = true
	}
}

// ================
// Lint configuration

detekt {
	toolVersion = '1.0.0-RC14'
	input = files("src/main/kotlin")
	filters = ".*/resources/.*,.*/build/.*"
	//baseline = file("my-detekt-baseline.xml")
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

// ================
// Test configuration

jacocoTestReport {
    reports {
        xml.enabled = true
        csv.enabled = false
    }
}

tasks.withType(Test) {
	exclude "**/*\$*", "**/*Tests.class", "**/examples/**"
	reports {
		html.enabled = true
		junitXml.outputPerTestCase = false
	}
	testLogging.exceptionFormat = 'full'
	beforeTest {
		logger.lifecycle("{} > {}", it.parent.name, it.name)
	}
}
